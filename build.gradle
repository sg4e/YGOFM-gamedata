plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

group = 'moe.maika'
version = '1.1.0'

compileJava {
    sourceCompatibility = '8'
    targetCompatibility = '8'
}

repositories {
    mavenCentral()
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
        showStandardStreams = true
    }
    useJUnitPlatform()
}

dependencies {
    //unit tests
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    //test logging
    testImplementation 'org.apache.logging.log4j:log4j-api:2.22.1'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.22.1'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j2-impl', version: '2.22.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype {  
            nexusUrl.set(uri("https://central.sonatype.com/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/"))
        }
    }
}

ext.genOutputDir = file("$buildDir/generated-resources")

task generateVersionTxt()  {
    ext.outputFile = file("$genOutputDir/version.txt")
    outputs.file(outputFile)
    doLast {
        outputFile.text = """GroupId: ${project.group}
Name: ${project.name}
Version: $version
Build-time: ${java.time.LocalDateTime.now()}
"""
    }
}

sourceSets.main.output.dir genOutputDir, builtBy: generateVersionTxt

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'ygofm-gamedata'
                description = 'API and simulation suite for game data of Yu-Gi-Oh! Forbidden Memories'
                url = 'https://github.com/sg4e/YGOFM-gamedata'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'sg4e'
                        name = 'sg4e'
                    }
                }
                scm {
                    url = 'https://github.com/sg4e/YGOFM-gamedata'
                    connection = 'scm:git://github.com/sg4e/YGOFM-gamedata.git'
                    developerConnection = 'scm:git://github.com/sg4e/YGOFM-gamedata.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
